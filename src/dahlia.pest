WHITESPACE = _{ " " | "\t" | NEWLINE }

COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

iden = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

number = @{ ASCII_DIGIT+ }

string_val = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

ty_float  = { "float" }
ty_double = { "double" }
ty_bool   = { "bool" }
ty_bit    = { "bit" ~ "<" ~ number ~ ">" }
ty_ubit   = { "ubit" ~ "<" ~ number ~ ">" }
ty_fix    = { "fix" ~ "<" ~ number ~ "," ~ number ~ ">" }
ty_ufix   = { "ufix" ~ "<" ~ number ~ "," ~ number ~ ">" }

ty_atom = {
    ty_float
  | ty_double
  | ty_bool
  | ty_bit
  | ty_ubit
  | ty_fix
  | ty_ufix
  | iden
}

ty_idx = { "[" ~ number ~ ("bank" ~ number)? ~ "]" }

ty = { ty_atom ~ (("{" ~ number ~ "}")? ~ ty_idx+)? }

expr_cast = { "(" ~ expr ~ "as" ~ ty_atom ~ ")" }

rec_lit_field = { iden ~ "=" ~ expr }

rec_in = _{ rec_lit_field ~ (";" ~ rec_lit_field)* }

arr_in = _{ expr ~ ("," ~ expr)* }

compound_literal = { "{" ~ (rec_in | arr_in) ~ "}" }

array_access = { iden ~ ("[" ~ expr ~ "]")+ }

rational = @{ "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ "." ~ ASCII_DIGIT+ }

hex = @{ "0x" ~ ASCII_HEX_DIGIT+ }

octal = @{ "0" ~ ASCII_OCT_DIGIT+ }

uint = @{ "0" | "-"? ~ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) }

boolean = { "true" | "false" }

app = { iden ~ (("(" ~ ")") | ("(" ~ expr ~ ("," ~ expr)* ~ ")")) }

primary = {
    expr_cast
  | compound_literal
  | rational
  | hex
  | octal
  | uint
  | boolean
  | array_access
  | app
  | iden
  | "(" ~ expr ~ ")"
}

atom = { primary ~ ("." ~ iden)* }

div    = { "/" }
mul    = { "*" }
modulo = { "%" }

add = { "+" }
sub = { "-" }

shr = { ">>" }
shl = { "<<" }

eq  = { "==" }
neq = { "!=" }
ge  = { ">=" }
le  = { "<=" }
gt  = { ">" }
lt  = { "<" }

band = { "&" }
bor  = { "|" }
bxor = { "^" }

and = { "&&" }
or  = { "||" }

infix_op = _{
    mul
  | div
  | modulo
  | add
  | sub
  | shl
  | shr
  | eq
  | neq
  | le
  | ge
  | lt
  | gt
  | and
  | or
  | band
  | bor
  | bxor
}

expr = { atom ~ (infix_op ~ atom)* }

let_stmt = { "let" ~ iden ~ (":" ~ ty)? ~ ("=" ~ expr)? }

assign     = { ":=" }
add_assign = { "+=" }
sub_assign = { "-=" }
mul_assign = { "*=" }
div_assign = { "/=" }

assign_op = {
    assign
  | add_assign
  | sub_assign
  | mul_assign
  | div_assign
}

// lvalue = { array_access | iden }

update = { expr ~ assign_op ~ expr }

view_suffix_underscore = { "_" }

view_suffix = { view_suffix_underscore | number ~ "*" ~ expr | expr ~ "!" }

view_prefix_opt = { ("+" ~ number)? }

view_shrink_opt = { ("bank" ~ number)? }

view_param = { "[" ~ view_suffix ~ ":" ~ view_prefix_opt ~ view_shrink_opt ~ "]" }

view = { "view" ~ iden ~ "=" ~ iden ~ view_param+ }

split_factor = { "[" ~ "by" ~ number ~ "]" }

split = { "split" ~ iden ~ "=" ~ iden ~ split_factor+ }

return = { "return" ~ expr }

simple_cmd = { let_stmt | update | split | return | view | expr }

par_cmd_item = { block_cmd | (simple_cmd ~ ";") }

par_cmd = { par_cmd_item* }

cmd = { par_cmd ~ ("---" ~ par_cmd)* }

block = { "{" ~ cmd ~ "}" }

else_block = { ("else" ~ block)? }

if_else = { "if" ~ "(" ~ expr ~ ")" ~ block ~ else_block }

kw_pipeline = { "pipeline" }

pipeline = { kw_pipeline? }

while_loop = { "while" ~ "(" ~ expr ~ ")" ~ pipeline ~ block }

kw_for_rev = { "rev" }

for_rev = { kw_for_rev? }

for_range = { "(" ~ "let" ~ iden ~ (":" ~ ty)? ~ "=" ~ for_rev ~ number ~ ".." ~ number ~ ")" ~ ("unroll" ~ number)? }

for_loop = { "for" ~ for_range ~ pipeline ~ block ~ combine_block }

combine_block = { ("combine" ~ block)? }

decor = { "decor" ~ string_val }

decors = { decor* }

block_cmd = { block | if_else | while_loop | for_loop | decor }

backend_cpp    = { "c++" }
backend_vivado = { "vivado" }
backend_futil  = { "futil" }
backend_calyx  = { "calyx" }

backend = { backend_cpp | backend_vivado | backend_futil | backend_calyx }

func_signature = { "def" ~ iden ~ func_args ~ (":" ~ ty)? ~ ";" }

func_signatures = { func_signature* }

backend_opt = { backend ~ "(" ~ string_val ~ ")" }

include = { "import" ~ backend_opt+ ~ "{" ~ func_signatures ~ "}" }

includes = { include* }

arg = { iden ~ ":" ~ ty }

func_args = {
    "(" ~ ")"
  | "(" ~ arg ~ ("," ~ arg)* ~ ")"
}

func_def = {
    "def" ~ iden ~ func_args ~ (":" ~ ty)? ~ "=" ~ block
}

rec_field_defs = {
    "{" ~ "}"
  | "{" ~ arg ~ (";" ~ arg)* ~ "}"
}

record_def = { "record" ~ iden ~ rec_field_defs }

decl  = _{ "decl" ~ arg ~ ";" }
decls =  { decl* }

def = { func_def | record_def }

defs = { def* }

prog_cmd = { cmd? }

prog = { SOI ~ includes ~ defs ~ decors ~ decls ~ prog_cmd ~ EOI }
